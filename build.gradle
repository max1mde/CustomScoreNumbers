plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

group = 'com.maximde'
version = '1.2.1'

def submoduleJars = []

subprojects { subproject ->
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.30'
        annotationProcessor 'org.projectlombok:lombok:1.18.30'
        implementation "net.kyori:adventure-text-minimessage:4.16.0"
    }
    repositories {
        mavenCentral()
        maven { url 'https://repo.codemc.io/repository/maven-releases/' }
        maven { url 'https://jitpack.io' }
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "papermc-repo"
            url = uri("https://repo.papermc.io/repository/maven-public/")
        }
    }
    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
        disableAutoTargetJvm()
    }

    tasks.compileJava {
        options.release.set(17)
    }

    shadowJar {
        exclude("**/assets/mappings/")
        exclude("**/META-INF/**")
    }

    if (subproject.name != 'core') {
        def shadowJarTask = subproject.tasks.getByName('shadowJar')
        def submoduleJar = shadowJarTask.archiveFile.get()
        submoduleJars.add(submoduleJar)
    }
}

tasks.register('crossPlatformJar', Jar) {
    dependsOn(subprojects.tasks.assemble)
    from(submoduleJars.collect { zipTree(it) })
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveFileName.set("CustomScores-${version}.jar")
}

tasks.named('shadowJar').configure {
    enabled = false
}